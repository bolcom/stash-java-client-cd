apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'

sourceCompatibility = 1.7
targetCompatibility = 1.7

group = 'com.bol.continuousdelivery'
version = '0.1.4'

ext {
    libraries = [:]
    versions = [:]
}

versions += [
        stashJavaClient: '1.3.6',
        feign          : '6.1.1',
        slf4j          : '1.7.6',
]

libraries += [
        jmockit: 'com.googlecode.jmockit:jmockit:1.6', //Not using 1.7, https://code.google.com/p/jmockit/issues/detail?id=343
        junit  : 'junit:junit:4.11',
]

libraries.feign = ["com.netflix.feign:feign-core:${versions.feign}",
                   "com.netflix.feign:feign-jackson:${versions.feign}",
                   "com.netflix.feign:feign-jaxrs:${versions.feign}",
]

libraries.slf4j = [
        "org.slf4j:jcl-over-slf4j:${versions.slf4j}",
		"org.slf4j:slf4j-api:${versions.slf4j}",
        "org.slf4j:slf4j-log4j12:${versions.slf4j}",
        "log4j:log4j:1.2.17"
]

jar {
    manifest {
        attributes 'Implementation-Title': 'Stash Java Client Continuous Deliver', 'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile libraries.feign
    compile libraries.slf4j

    testCompile 'commons-io:commons-io:2.4'
    testCompile libraries.junit
    testCompile libraries.jmockit
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}


